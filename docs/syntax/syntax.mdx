---
slug: /syntax
---
import SnapBlocks from '@site/src/components/SnapBlocks.js'

This article demonstrates the **[Block Plugin](Block_Plugin "wikilink") syntax**.

On the [Scratch Forums](Scratch_Discussion_Forums "wikilink"), code must be written between `[scratchblocks]...[/scratchblocks]` tags. (On the wiki, `<snapblocks>...</snapblocks>` tags are used.) For a simplified explanation, see [here](topic:54562 "wikilink").

Every [block](block "wikilink") goes on a new line and is written as the text on the block is shown on Scratch. For example:

<table>
<thead>
<tr>
<th><p>Code</p></th>
<th><p>Result</p></th>
</tr>
</thead>
<tbody>
<tr>
<td>
```
when green flag clicked
forever {
    turn cw (15) degrees
    say [Hello!] for (2) seconds
    if \<mouse down?\> {
        change [mouse clicks V] by (1)
    }
}
```
</td>
<td><SnapBlocks>
{`when green flag clicked
forever {
    turn cw (15) degrees
    say [Hello!] for (2) seconds
    if \<mouse down?\> {
        change [mouse clicks V] by (1)
    }
}`}
</SnapBlocks></td>
</tr>
</tbody>
</table>

## Arguments

[Arguments](Arguments "wikilink"), or inputs to a block, are represented
within the block with various codes.

### Numerical Insert

The round numerical insert is used with parentheses: `(10)`.

```
move (10) steps
```

<SnapBlocks>{`move (10) steps`}</SnapBlocks>

### String Insert

[String](String "wikilink") inputs are created with square brackets:
`[lorem ipsum]`

```
say [Hi]
```

<SnapBlocks>{`say [Hi]`}</SnapBlocks>

```
think [bye]
```
<SnapBlocks>{`think [bye]`}</SnapBlocks>

### Block Insert

Boolean blocks and reporter
blocks are created with `<boolean>` and
`(reporter)`, respectively.

```
if <<mouse down?> and <(costume [number v]) = [1]>> {
    stamp
}
```

<SnapBlocks>
{`if <<mouse down?> and <(costume [number v]) = [1]>> {
    stamp
}`}
</SnapBlocks>

### Color Input

Color inputs can be made by entering a hexadecimal code into a string
input.

```
set pen color to [#1540bf]
```

<SnapBlocks>{`set pen color to [#1540bf]`}</SnapBlocks>

Note that 3-byte long hexadecimal codes are also supported.

```
set pen color to [#0f0]
```

<SnapBlocks>{`set pen color to [#0f0]`}</SnapBlocks>

### Dropdown Inputs

Dropdown inputs are created by adding a `V` at the end of a text or number input.

```
stop [all V]
item (last v) of @list
```

<SnapBlocks>
{`stop [all V]
item (last v) of @list`}
</SnapBlocks>

Readonly dropdowns are created with a capital `V`, whereas editable dropdowns are created with a lowercase `v`.

```
([length V] of text [Hello world])
(split [Hello world] by [word v])
(item (random v) of @list)
(readonly number (1 V))
```

<SnapBlocks>
{`([length V] of text [Hello world])
(split [Hello world] by [word v])
(item (random v) of @list)
(readonly number (1 V))`}
</SnapBlocks>

## Special Blocks

Some blocks have different code based on their unique shapes and
features.

### Hat Blocks

The When Green Flag Clicked block can be typed with any of the following syntax options:

```
when green flag clicked
when gf clicked
when flag clicked
when @greenFlag clicked
```

<SnapBlocks>{`when gf clicked`}</SnapBlocks>

### Stack Blocks

The Turn () Degrees (clockwise) block can be written two ways:

```
turn cw () degrees
turn right () degrees
```

<SnapBlocks>{`turn cw () degrees`}</SnapBlocks>

The Turn () Degrees (counter-clockwise) block can also be written two ways:

```
turn ccw () degrees
turn left () degrees
```

<SnapBlocks>{`turn ccw () degrees`}</SnapBlocks>

### C Blocks

The blocks inside a C Block must be inside curly brackets `{}`. It will automatically close if you don't include a closed curly bracket `}` For example:

```
repeat (10) {
    move (5) steps
    stamp
}
repeat (10) {
    move (10) steps
    stamp
```

Makes:

<SnapBlocks>
{`repeat (10) {
    move (5) steps
    stamp
}
repeat (10) {
    move (10) steps
    stamp`}
</SnapBlocks>

## Comments

Comments are created with two slashes: `// comment` after a block.

```
move (10) steps // is that too far?
```

<SnapBlocks>{`move (10) steps // is that too far?`}</SnapBlocks>

## Custom Blocks

If one tries to show a custom block, it will appear obsolete (red)
because it has not been defined.

```
jump
```

<SnapBlocks>{`jump`}</SnapBlocks>

## Scratch Custom Blocks
There are differences between Scratch custom blocks and Snap<i>!</i> custom blocks.

A definition can be created by writing "define" followed by the name of the block inside curly brackets:

```
define {jump}
repeat (10) {
    change y by (4)
}
```

<SnapBlocks>
{`define {jump}
repeat (10) {
    change y by (4)
}`}
</SnapBlocks>

The brackets were not needed in scratchblocks, but since Snap<i>!</i> has a block with the label `define`, and scratchblocks forced the special formatting with no way to turn it off. This was specifically changed in snapblocks to require curly brackets to avoid these issues. If you want to keep the old syntax, you can add the `define` override.

```
define jump :: define
repeat (10) {
    change y by (4)
}
```

<SnapBlocks>
{`define jump :: define
repeat (10) {
    change y by (4)
}`}
</SnapBlocks>

Number, boolean, and string arguments can be added:

```
define {jump (height) <gravity on?> [message]}
```

<SnapBlocks>{`define {jump (height) <gravity on?> [message]}`}</SnapBlocks>

Once a define hat has been made, one can then use the block *inside the
same \<scratchblocks\> tag*, and it will no longer appear obsolete.

```
jump

define jump
repeat (10) {
    change y by (4)
}
```

<SnapBlocks>
{
    `jump

    define jump
    repeat (10) {
        change y by (4)
    }`}
</SnapBlocks>

### Custom Block Inputs

If one tries to use an input reporter without making a block definition
first, it will appear as a [variable](variable "wikilink").

```
say (height)
```

<SnapBlocks>{`say (height)`}</SnapBlocks>

But if it is put below a block definition, it will render as an input
reporter:

```
define jump (height)
say (input)
```

<SnapBlocks> {`define jump (height) say (height)`} </SnapBlocks>

## List Reporters

If one tries to write a list reporter, it will look like a variable
reporter, because the plugin has no way of telling them apart.

```
say (list of Scratch team members)
```

<SnapBlocks>{`say (list of Scratch team members)`}</SnapBlocks>

However, if one has used the list in a **list block** *inside the same
\<scratchblocks\> tag*, then it will render correctly:

```
add [mres] to [list of Scratch team members v]
add [paddle2see] to [list of Scratch team members v]
add [harakou] to [list of Scratch team members v]
say (list of Scratch team members)
```

<SnapBlocks>
add [mres] to [list of Scratch team members v]
add [paddle2see] to [list of Scratch team members v]
add [harakou] to [list of Scratch team members v]
say (list of Scratch team members)
</SnapBlocks>

If a list block is not wanted or needed inside the same
\<scratchblocks\> tag, `:: list` can be used:

```
say (list of Scratch team members:: list)
```

<SnapBlocks>
{`say (list of Scratch team members:: list)`}
</SnapBlocks>

## Shortening Source Code

It is possible to make the source of ScratchBlocks code slightly shorter
by removing unnecessary code. No spaces are necessary between an insert
and the block text. Also, closing brackets (`]`) and parentheses (`)`)
can be left off at the end of a line. Therefore, the following two
snippets render identically, though the first is 226 characters, and the
second only 183:

```
when gf clicked
ask [n=] and wait
set [n v] to (answer)
set [i v] to [0]
repeat until <(n) = [1]>
if <((n) mod (2)) = [0]> then
set [n v] to ((n) / (2))
else
set [n v] to (((3) * (n)) + (1))
end
change [i v] by (1)
end
say (i)

when gf clicked
ask[n=]and wait
set[n v]to(answer
set[i v]to[0
repeat until<(n)=[1
if<((n)mod(2))=[0]>then
set[n v]to((n)/(2
else
set[n v]to(((3)*(n))+(1
end
change[i v]by(1
end
say(i
```

<SnapBlocks>
{
    `when gf clicked
ask [n=] and wait
set [n v] to (answer)
set [i v] to [0]
repeat until <(n) = [1]>
if <((n) mod (2)) = [0]> then
set [n v] to ((n) / (2))
else
set [n v] to (((3) * (n)) + (1))
end
change [i v] by (1)
end
say (i)

when gf clicked
ask[n=]and wait
set[n v]to(answer
set[i v]to[0
repeat until<(n)=[1
if<((n)mod(2))=[0]>then
set[n v]to((n)/(2
else
set[n v]to(((3)*(n))+(1
end
change[i v]by(1
end
say(i`
}</SnapBlocks>

However, this is considered poor style and is not recommended.

## Hacks

The blocks plugin offers hacks to allow representation of scripts from
[Scratch Modifications](Scratch_Modifications "wikilink") and old or
unreleased versions of Scratch.

For a full tutorial on how to use scratchblocks hacks, see [this
topic](topic:55586 "wikilink").

### Color and Shape Changing

The color and shape of a block can be changed. This can be useful for
forcing non-Scratch blocks to appear correctly. Note that only color
changing with the legacy syntax works in the old version. The legacy
syntax is obsolete (it no longer works since scratchblocks3).

<table>
<colgroup>
<col style="width: 30%" />
<col style="width: 50%" />
<col style="width: 20%" />
</colgroup>
<thead>
<tr>
<th width="30%"><p>Feature</p></th>
<th width="50%"><p>Code</p></th>
<th width="20%"><p>Result</p></th>
</tr>
</thead>
<tbody>
<tr>
<td><p>Changing category (works for any kind of block)</p></td>
<td>
```
abc:: looks
say [I'm not a Motion block!]::motion
eat (pen color:: pen):: control
if <touching (mouse pointer v)?:: list> then
   die:: grey
end
```
</td>
<td>
<SnapBlocks>
{`abc:: looks
say [I'm not a Motion block!]::motion
eat (pen color:: pen):: control
if <touching (mouse pointer v)?:: list> then
   die:: grey
end`}
</SnapBlocks></td>
</tr>
<tr>
<td><p>Changing color</p></td>
<td>
```
think [Arbitrary colors?]:: #228b22
```
</td>
<td>
<SnapBlocks>
{`think [Arbitrary colors?]::#228b22`}
</SnapBlocks></td>
</tr>
<tr>
<td><p>Changing shape</p></td>
<td>
```
abc:: events hat
def:: motion stack
ghi:: pen reporter
jkl:: operators boolean
```
</td>
<td>
<SnapBlocks>
{`abc:: events hat def:: motion stack ghi:: pen
reporter jkl:: operators boolean`}
</SnapBlocks>
</td>
</tr>
<tr>
<td><p>Creating C blocks and changing category</p></td>
<td>
```
mno {
    ...
}:: sensing
```
</td>
<td>
<SnapBlocks>
{`mno {
    ...
}:: sensing`}
</SnapBlocks>
</td>
</tr>
<tr>
<td><p>C blocks with multiple branches</p></td>
<td>
```
pqr {
    ...
} stu {
    ...
} vwx:: sound
```
</td>
<td>
<SnapBlocks>
{`pqr {
   ...
} stu {
   ...
} vwx:: sound`}
</SnapBlocks>
</td>
</tr>
<tr>
<td><p>C block with cap</p></td>
<td>
```
yz {
    ...
}:: motion cap
```
</td>
<td>
<SnapBlocks>
{`yz {
   ...
}:: motion cap`}
</SnapBlocks></td>
</tr>
<tr>
<td><p>Adding icons</p></td>
<td>
```
@greenFlag @stopSign @turnRight @loopArrow:: grey
```
</td>
<td>
<SnapBlocks>
{`@greenFlag @stopSign @turnRight @loopArrow:: grey`}
</SnapBlocks></td>
</tr>
</tbody>
</table>

### Snap*!*

The new plugin also supports features specific to Snap*!*, such as
"rings". Other blocks in Snap*!* can be created using the color/shape
hacks above.

```
run ({create clone:: control} @addInput:: grey ring):: control

<() @addInput:: grey ring>

say (http:// [snap.berkeley.edu]:: sensing)

((6) × (7):: operators)

(join [hello ] [world] @delInput @addInput:: operators)

script variables ((foo):: grey) ((bar):: grey) @delInput @addInput:: grey

warp {
    move (10) steps
} :: grey

report [Done!]:: control cap

(<> @addInput) // without even the:: grey ring
```

which produces these blocks:

<SnapBlocks>
{`run ({create clone:: control} @addInput:: grey ring):: control

<() @addInput:: grey ring>

say (http:// [snap.berkeley.edu]:: sensing)

((6) × (7):: operators)

(join [hello ] [world] @delInput @addInput:: operators)

script variables ((foo):: grey) ((bar):: grey) @delInput @addInput:: grey

warp {
    move (10) steps
} :: grey

report [Done!]:: control cap

(<> @addInput) // without even the:: grey ring`}
</SnapBlocks>

### GP

This plugin also includes hacks to [GP](GP "wikilink").

<SnapBlocks>
{`when tracking (mouse x:: variables) (mouse y:: variables):: hat control

((1) !=\\ (0):: operators)

define(a function:: motion):: custom hat return (1):: control

say (pi:: operators):: motion

when I receive \[go\]:: control

wait @addInput:: control

(isNil \[obj v\]:: operators)

(touching mouse:: sensing)

((--(:: \#fff):: operators):: operators)

if (--(:: \#fff):: operators) { } @addInput:: control`}
</SnapBlocks>

## Backslash

A backslash (\\ is a character that cancels out any special
functionality of the next character, making it show up as normal text.
If a character with special functionality, like a closing bracket (\]),
needs to be rendered as normal text, put a backslash before it.

```
say []]
```

<SnapBlocks>{`say \[\]\]`}</SnapBlocks>

```
say [\]]
```

<SnapBlocks>{`say \[\\\]`}</SnapBlocks>

This is useful in certain situations, where backslashes are necessary to
properly display a block:

```
play drum (\(1\) Snare Drum v) for (0.25) beats
```

<SnapBlocks>
{`play drum ($1$ Snare Drum v) for (0.25) beats`}
</SnapBlocks>

[fr:Scratch Wiki Accueil/le forum/blocs du forum/liste
blocs](fr:Scratch_Wiki_Accueil/le_forum/blocs_du_forum/liste_blocs "wikilink")

[Category:Scratch Extensions](Category:Scratch_Extensions "wikilink")
